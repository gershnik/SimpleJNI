import org.jetbrains.kotlin.gradle.internal.KaptWithoutKotlincTask

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.jetbrains.kotlin.kapt)
}

//JniGen settings
def jniGenProps = new Object() {
    def generatedPath = file("src/main/cpp/generated").absolutePath
    def outputListName = "outputs.txt"
    def additionalClasses = ["java.lang.Byte"]
}

dependencies {

    //JNI annotations
    compileOnly(libs.smjni.jnigen.annotations)
    //JNI code generator
    kapt(libs.smjni.jnigen.processor)

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.android.material)
    implementation(libs.androidx.constraintlayout)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
}

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    namespace 'com.example.myapplication'

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DJNIGEN_GENERATED_DIR=${file(jniGenProps.generatedPath).parentFile.absolutePath}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro',
                    '../../../common-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlin {
        jvmToolchain(8)
    }
    externalNativeBuild {
        cmake {
            path file('../../../cpp/CMakeLists.txt')
            version libs.versions.cmake.get()
        }
    }
}

kapt {
    useBuildCache = false
    arguments {
        arg("smjni.jnigen.dest.path", jniGenProps.generatedPath)
        arg("smjni.jnigen.own.dest.path", "true")
        arg("smjni.jnigen.output.list.name", jniGenProps.outputListName)
        arg("smjni.jnigen.expose.extra", jniGenProps.additionalClasses.join(";").toString())
    }
}

tasks.withType(KaptWithoutKotlincTask).configureEach {
    outputs.upToDateWhen {

        def jniGenOutputList = file("${jniGenProps.generatedPath}/${jniGenProps.outputListName}")

        if (!jniGenOutputList.exists()) {
            return false
        }

        for(line in jniGenOutputList) {
            if (!file("${jniGenProps.generatedPath}/$line").exists()) {
                return false
            }
        }
        return true
    }
}



//Clean generated headers on project clean
tasks.register('cleanJNIHeaders', Delete) {
    group = 'build'
    delete file("${jniGenProps.generatedPath}")
}
clean.dependsOn cleanJNIHeaders

//Make KAPT (and so JniGen code generation) run before CMake build
tasks.configureEach { theTask ->
    def match = theTask.name =~ ~/^buildCMake([^\[]*).*$/
    if (match) {
        def config
        switch(match.group(1)) {
            case "RelWithDebInfo": config = "Release"; break
            default: config = match.group(1); break
        }
        theTask.dependsOn "kapt${config}Kotlin"
    }
}
